plugins {
    id 'java'
    id 'org.asciidoctor.jvm.gems' version '4.0.0'
    id 'org.asciidoctor.jvm.convert' version '4.0.0' apply false
    id 'org.asciidoctor.jvm.pdf' version '4.0.0'
    id 'org.asciidoctor.jvm.epub' version '4.0.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id 'com.github.ben-manes.versions' version '0.50.0'
}

repositories {
    mavenCentral()
    ruby {
        gems()
    }
}

/**
 * Take the version in version.txt as version for the project
 */
version = file('version.txt').text.trim()

asciidoctorj {
    version = '2.5.7'
    requires file('src/main/ruby/asciidoctor-pdf-extensions.rb')
    modules {
        pdf {
            version '2.3.7'
        }
    }
}

import org.asciidoctor.gradle.jvm.AsciidoctorTask

def attrs = ['sourcedir'         : '../../../main/webapp',
             'source-highlighter': 'rouge',
             'epub3-stylesdir'   : './styles/epub',
             // NOTE don't include leading ./ because it messes up paths in the epub files
             'imagesdir'         : 'images',
             'toc'               : 'left',
             'icons'             : 'font',
             'sectanchors'       : '',
             'idprefix'          : '',
             'idseparator'       : '-',
             'listing-caption'   : 'Listing']

tasks.withType(AsciidoctorTask) { task ->
    attributes attrs
    sourceDir 'src/docs/asciidoc/modules/ROOT'
    baseDirFollowsSourceDir()
    sources {
        include 'index.adoc'
    }
}

task html(type: AsciidoctorTask, description: 'Generates single page HTML') {
    outputDir "$buildDir/asciidoc/html5"
    outputOptions {
        backends 'html5'
    }
}

task prepress(type: AsciidoctorTask, description: 'Generates PDF for prepress printing') {
    attributes attrs + ['media': 'prepress', 'pdfmarks': '', 'pdf-theme': 'prepress', 'source-highlighter': '']
    outputDir "$buildDir/asciidoc/pdf-prepress"
    outputOptions {
        backends 'pdf'
        setSeparateOutputDirs false
    }
}

task pdf(type: AsciidoctorTask, description: 'Generates PDF for download') {
    attributes attrs + ['pdfmarks': '']
    outputDir "$buildDir/asciidoc/pdf-screen"
    outputOptions {
        backends 'pdf'
        setSeparateOutputDirs false
    }
}

// Rouge doesn't work in EPUB, so use CodeRay
task epub(type: AsciidoctorTask, description: 'Generates EPUB3') {
    attributes attrs + ['source-highlighter': 'coderay']
    outputDir "$buildDir/asciidoc/epub3"
    outputOptions {
        backends 'epub3'
    }
}

pdf.shouldRunAfter html
prepress.shouldRunAfter pdf
epub.shouldRunAfter prepress

task all(dependsOn: ['html', 'pdf', 'prepress', 'epub'])

defaultTasks 'all'
