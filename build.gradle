buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.39.0'
    }
}

plugins {
    id 'org.asciidoctor.jvm.gems' version '3.3.2'
    id 'org.asciidoctor.jvm.convert' version '3.3.2' apply false
    id 'org.asciidoctor.jvm.pdf' version '3.3.2'
    id 'org.asciidoctor.jvm.epub' version '3.3.2'
    id 'de.undercouch.download' version '4.1.2'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    mavenCentral()
    ruby {
        gems()
    }
}

/**
 * Take the version in version.txt as version for the project
 */
version = file('version.txt').text.trim()

asciidoctorj {
    version = '2.5.2'
    requires file('src/main/ruby/asciidoctor-pdf-extensions.rb')
}

import org.asciidoctor.gradle.jvm.AsciidoctorTask

def attrs = ['sourcedir'         : '../../../main/webapp',
             'source-highlighter': 'rouge',
             'epub3-stylesdir'   : './styles/epub',
             // NOTE don't include leading ./ because it messes up paths in the epub files
             'imagesdir'         : 'images',
             'toc'               : 'left',
             'icons'             : 'font',
             'sectanchors'       : '',
             'idprefix'          : '',
             'idseparator'       : '-',
             'listing-caption'   : 'Listing']

tasks.withType(AsciidoctorTask) { task ->
    attributes attrs
    sourceDir 'src/docs/asciidoc'
    baseDirFollowsSourceDir()
    sources {
        include 'index.adoc'
    }
}

task html(type: AsciidoctorTask, description: 'Generates single page HTML') {
    outputDir "$buildDir/asciidoc/html5"
    outputOptions {
        backends 'html5'
    }
}

task prepress(type: AsciidoctorTask, description: 'Generates PDF for prepress printing') {
    attributes attrs + ['media' : 'prepress', 'pdfmarks': '', 'pdf-theme': 'prepress']
    attrs.remove('source-highlighter')
    outputDir "$buildDir/asciidoc/pdf-prepress"
    outputOptions {
        backends 'pdf'
        setSeparateOutputDirs false
    }
}

task pdf(type: AsciidoctorTask, description: 'Generates PDF for download') {
    attributes attrs + ['pdfmarks': '', 'source-highlighter': 'rouge']
    outputDir "$buildDir/asciidoc/pdf-screen"
    outputOptions {
        backends 'pdf'
        setSeparateOutputDirs false
    }
}

task epub(type: AsciidoctorTask, description: 'Generates EPUB3') {
    outputDir "$buildDir/asciidoc/epub3"
    outputOptions {
        backends 'epub3'
    }
}

pdf.shouldRunAfter html
prepress.shouldRunAfter pdf
epub.shouldRunAfter prepress

task all(dependsOn: ['html', 'pdf', 'prepress', 'epub'])

defaultTasks 'all'
