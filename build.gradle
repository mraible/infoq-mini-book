buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.39.0'
        classpath 'org.asciidoctor:asciidoctor-gradle-jvm:3.3.2'
        classpath 'org.asciidoctor:asciidoctorj-epub3:1.5.1'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.6.0'
        classpath 'org.jruby:jruby-complete:9.2.19.0'
    }
}

plugins {
    id 'de.undercouch.download' version '4.1.2'
    id 'se.patrikerdes.use-latest-versions' version '0.2.17'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

apply plugin: 'java'
apply plugin: 'org.asciidoctor.jvm.convert'
apply plugin: 'com.github.ben-manes.versions'

/**
 * Take the version in version.txt as version for the project
 */
version = file('version.txt').text.trim()

asciidoctorj {
    version = '3.2.0'
}

import org.asciidoctor.gradle.jvm.AsciidoctorTask

def attrs = ['sourcedir'         : '../../../main/webapp',
             'source-highlighter': 'rouge',
             'epub3-stylesdir'   : './styles/epub',
             // NOTE don't include leading ./ because it messes up paths in the epub files
             'imagesdir'         : 'images',
             'toc'               : 'left',
             'icons'             : 'font',
             'sectanchors'       : '',
             'idprefix'          : '',
             'idseparator'       : '-',
             'listing-caption'   : 'Listing']

tasks.withType(AsciidoctorTask) { task ->
    attributes attrs
    sources {
        include 'index.adoc'
    }
}

task html(type: AsciidoctorTask, description: 'Generates single page HTML') {
    outputOptions {
        backends 'html5'
    }
}

task prepress(type: AsciidoctorTask, description: 'Generates PDF for prepress printing') {
    attributes attrs + ['media' : 'prepress', 'pdfmarks': '', 'pdf-theme': 'prepress']
    attrs.remove('source-highlighter')
    requires file('src/main/ruby/asciidoctor-pdf-extensions.rb')
    outputDir '$buildDir/asciidoc/pdf-prepress'
    outputOptions {
        backends 'pdf'
        separateOutputDirs false
    }
}

task pdf(type: AsciidoctorTask, description: 'Generates PDF for download') {
    attributes attrs + ['pdfmarks': '', 'source-highlighter': 'rouge']
    requires file('src/main/ruby/asciidoctor-pdf-extensions.rb')
    outputDir '$buildDir/asciidoc/pdf-screen'
    outputOptions {
        backends 'pdf'
        separateOutputDirs false
    }
}

task epub(type: AsciidoctorTask, description: 'Generates EPUB3') {
    outputOptions {
        backends 'epub3'
    }
}

pdf.shouldRunAfter html
prepress.shouldRunAfter pdf
epub.shouldRunAfter prepress

task all(dependsOn: ['html', 'pdf', 'prepress', 'epub'])

defaultTasks 'all'
